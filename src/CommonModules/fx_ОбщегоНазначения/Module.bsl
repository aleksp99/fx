
#Область ПрограммныйИнтерфейс

// @skip-check method-too-many-params
// 
// Параметры:
//  Элемент1 - Произвольный - 1ое значение, которое необходимо поместить  в ХЕШ сумму 
//  Элемент2 - Произвольный - (необязательный), 2ое значение, которое необходимо поместить  в ХЕШ сумму
//  Элемент3 - Произвольный - (необязательный), 3ое значение, которое необходимо поместить  в ХЕШ сумму
//  Элемент4 - Произвольный - (необязательный), 4ое значение, которое необходимо поместить  в ХЕШ сумму
//  Элемент5 - Произвольный - (необязательный), 5ое значение, которое необходимо поместить  в ХЕШ сумму
// 
// Возвращаемое значение:
//  Число, ДвоичныеДанные - Хеш сумма
Функция ХешСумма(Элемент1, 
	Элемент2 = "440e3512-0df8-4081-a10d-9dc877fbe625",
	Элемент3 = "440e3512-0df8-4081-a10d-9dc877fbe625", 
	Элемент4 = "440e3512-0df8-4081-a10d-9dc877fbe625", 
	Элемент5 = "440e3512-0df8-4081-a10d-9dc877fbe625") Экспорт

	Хеш = Новый ХешированиеДанных(ХешФункция.CRC32);
	Хеш.Добавить(ХешЗначение(Элемент1));
	Если Элемент2 <> "440e3512-0df8-4081-a10d-9dc877fbe625" Тогда
		Хеш.Добавить(ХешЗначение(Элемент2));
	КонецЕсли;
	Если Элемент3 <> "440e3512-0df8-4081-a10d-9dc877fbe625" Тогда
		Хеш.Добавить(ХешЗначение(Элемент3));
	КонецЕсли;
	Если Элемент4 <> "440e3512-0df8-4081-a10d-9dc877fbe625" Тогда
		Хеш.Добавить(ХешЗначение(Элемент4));
	КонецЕсли;
	Если Элемент5 <> "440e3512-0df8-4081-a10d-9dc877fbe625" Тогда
		Хеш.Добавить(ХешЗначение(Элемент5));
	КонецЕсли;
	
	Возврат Хеш.ХешСумма;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ХешЗначение(Значение)

	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Возврат НРег(СокрЛП(Значение));

	ИначеЕсли ТипЗнч(Значение) = Тип("Число") Или ТипЗнч(Значение) = Тип("Дата") Или ТипЗнч(Значение) = Тип("Булево")
		Или ТипЗнч(Значение) = Тип("УникальныйИдентификатор") Тогда
		Возврат XMLСтрока(Значение);

	ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Значение)) Тогда
		Возврат Лев(СтрРазделить(ЗначениеВСтрокуВнутр(Значение), ",")[2], 35);
		
	Иначе
		Возврат ЗначениеВСтрокуВнутр(Значение);

	КонецЕсли;

КонецФункции

#КонецОбласти
